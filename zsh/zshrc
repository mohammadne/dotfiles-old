# --------------------------------------------------------------------------------------- Tmux

# tmux new session (create a new one)
function tns() {
  session_name=$1

  # get basename of the current directory
  if [ -z "$session_name" ]; then session_name=${PWD##*/}; fi

  # create a new new-session based on current working directory
  if ! tmux has-session -t "$session_name" 2> /dev/null; then
    TMUX='' tmux new-session -s "$session_name" -d
  fi

  # attach if outside of tmux, switch if you're in tmux.
  if [ -z "$TMUX" ]; then
    tmux attach -t "$session_name"
  else
    tmux switch-client -t "$session_name"
  fi
}

# tmux select session (delete current one)
function tss() {
  # use built in choose-tree if being inside a session
  if [ ! -z "$TMUX" ]; then (tmux choose-tree -Zs &); return; fi

  # get session names (ignore error if no session exist)
  sessions=$(tmux ls -F '#S' 2> /dev/null)

  # return if no session exist
  if [ -z "$sessions" ]; then echo "there is no session available"; return; fi

  # select a session between sessions
  select session_name in $sessions; do break; done

  # return in case of invalid selection
  if [ -z "$session_name" ]; then echo "invalid session selected"; exit 1; fi

  # switch into new session
  tns "$session_name"
}

# tmux kill session (delete current one)
function tks() {
  session_name=$1

  # set session_name if not given
  if [ -z "$session_name" ]; then
    # return if not in a session
    if [ -z "$TMUX" ]; then return 0; fi

    # get current session name
    session_name=$(tmux display-message -p '#S')
  fi

  # kill session_name if available
  tmux kill-session -t "$session_name" 2> /dev/null
}

tns

# --------------------------------------------------------------------------------------- P10K

# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# --------------------------------------------------------------------------------------- Plugins

export ZSH="$HOME/.config/oh-my-zsh"

ZSH_THEME="powerlevel10k/powerlevel10k" # Set oh-my-zsh theme

# list of all plugins: https://github.com/ohmyzsh/ohmyzsh/wiki/Plugins
plugins=(
  # zsh-users plugins
  history-substring-search # ZSH port of Fish history search. Begin typing command, use up arrow to select previous use
  zsh-autosuggestions # Suggests commands based on your history
  zsh-completions # More completions
  zsh-syntax-highlighting # Fish shell like syntax highlighting for Zsh
  
  # theming
  colored-man-pages
  
  # development plugins
  git
  docker
  kubectl
  ansible
  vagrant
  
  # programming languages
  golang # go language support
  python
  rust
)

autoload -U compinit && compinit # reload completions for zsh-completions

export ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE='fg=5' # Colorize autosuggest

source $ZSH/oh-my-zsh.sh # required to load oh-my-zsh

# --------------------------------------------------------------------------------------- Development

# add homebrew path on MacOS
if [[ "$(uname)" = 'Darwin' ]]; then
  PATH=$PATH:/opt/homebrew/bin
fi

# add cargo if cargo has been installed
if command -v cargo &> /dev/null; then
  export CARGOPATH=~/.cargo/bin
  PATH=$PATH:$CARGOPATH
fi

# add gopath if go has been installed
if command -v go &> /dev/null; then
  export GOPATH=$(go env GOPATH)
  PATH=$PATH:$GOPATH
fi

# export $PATH
export PATH=$PATH:$HOME/.local/bin

# --------------------------------------------------------------------------------------- Miscellaneous

# ssh related
alias sshkey="xclip -selection clipboard < ~/.ssh/id_ed25519.pub && echo 'SSH Public Key copied to clipboard'"

# vpn related
alias snappvpnup="nmcli con up id snapp"
alias snappvpndown="nmcli con down id snapp"

# zsh related
alias zshconfig="nvim ~/.zshrc"
alias ohmyzsh="nvim ~/.oh-my-zsh"

# --------------------------------------------------------------------------------------- Miscellaneous

# This speeds up pasting w/ autosuggest
# https://github.com/zsh-users/zsh-autosuggestions/issues/238
pasteinit() {
  OLD_SELF_INSERT=${${(s.:.)widgets[self-insert]}[2,3]}
  zle -N self-insert url-quote-magic # I wonder if you'd need `.url-quote-magic`?
}

pastefinish() {
  zle -N self-insert $OLD_SELF_INSERT
}

zstyle :bracketed-paste-magic paste-init pasteinit
zstyle :bracketed-paste-magic paste-finish pastefinish

# To customize prompt, run `p10k configure` or edit ~/.config/p10k.zsh.
[[ ! -f ~/.config/p10k.zsh ]] || source ~/.config/p10k.zsh
